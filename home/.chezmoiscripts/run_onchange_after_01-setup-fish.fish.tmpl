#!{{ lookPath "fish" }}

# vim:ft=fish
{{ $hash := include "dot_config/fish/fish_plugins.tmpl" | sha256sum }}

source "{{ .chezmoi.sourceDir }}/.chezmoiscripts/.00_helpers.fish"

ensure_homebrew

set fish $HOMEBREW_PREFIX/bin/fish
set username {{ quote .chezmoi.username }}

function getshell
    if is_macos
        dscl . read "/Users/$username" UserShell | string replace -r '^UserShell: (.*)' '$1'
    else
        getent passwd $username | string replace -r '.*:([^:]+)$' '$1'
    end
end

function setshell -a shell
    if is_macos
        sudo dscl . create "/Users/$username" UserShell $shell
    else
        sudo chsh -s $shell $username
    end
end

if test (getshell) != $fish
    start "Setting up Fish üê†"
    ask_sudo
    start "Changing default shell to fish"

    if not string match -eq (status fish-path) </etc/shells
        echo "Adding $fish to /etc/shells..."
        printf '%s\n' "$fish" | sudo tee -a /etc/shells >/dev/null
    end

    setshell $fish
end

if type -q fisher; and test -s $HOME/.config/fish/fish_plugins

    # Set fisher install to be outside of ~/.config/fish
    # This variable is used at the top of fisher.fish
    set fisher_path $HOME/.cache/fish

    if not set -q fish_plugins_hash; or test $fish_plugins_hash != {{ $hash }}
        start "Running 'fisher update' üê†"

        fisher update >/dev/null 2>&1

        set -U fish_plugins_hash {{ $hash }}
    end
end

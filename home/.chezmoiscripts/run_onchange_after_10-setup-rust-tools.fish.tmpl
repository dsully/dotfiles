#!{{ lookPath "fish" }}

# Install Rust tools.
# vim:ft=fish

set CHEZMOI_SOURCE_DIR (chezmoi source-path)
source "$CHEZMOI_SOURCE_DIR/.chezmoiscripts/.00_helpers.fish"

if type -q cargo

    # Install packages via Cargo.
    set -l PACKAGES \
        # https://github.com/Timmmm/autorebase
        autorebase \
        # https://github.com/RustSec/rustsec/tree/main/cargo-audit
        cargo-audit \
        # https://github.com/cargo-bins/cargo-binstall
        cargo-binstall \
        # https://github.com/matthiaskrgr/cargo-cache
        cargo-cache \
        # https://github.com/Riey/cargo-feature
        cargo-feature \
        # https://nexte.st
        cargo-nextest \
        # https://github.com/birkenfeld/fddf
        fddf \
        # https://github.com/gitext-rs/git-dive
        git-dive \
        # https://github.com/nguyenvukhang/gitnu - git status wrapper.
        gitnu \
        # https://github.com/scruffystuffs/jfmt.rs
        jfmt \
        # https://github.com/acidnik/ppcp - Remove in favor of xcp?
        ppcp \
        # https://github.com/tarka/xcp/
        xcp

    if is_linux
        # https://github.com/Canop/lfs
        if not type -q lfs
            set -a PACKAGES lfs
        end
    end

    # These packages don't map to their CLI name.
    # https://github.com/killercup/cargo-edit
    if not type -q cargo-add
        set -a PACKAGES cargo-edit
    end

    # https://github.com/nabijaczleweli/cargo-update
    if not type -q cargo-install-update
        set -a PACKAGES cargo-update
    end

    task "Installing Rust tools. ðŸ¦€"

    for package in $PACKAGES
        sub_task "Installing $package"

        if not type -q $package
            command cargo --quiet install $package
        end
    end

    # My tools / forked / git-only.
    set -l URLS \
        git+ssh://git@github.com/dsully/git-open \
        git+ssh://git@github.com/dsully/lolcate-rs \
        git+ssh://git@github.com/dsully/zoned

    if is_macos
        set -a URLS git+https://github.com/alexwlchan/safari.rs
    end

    echo

    for url in $URLS
        sub_task "Installing $url" "â—†"

        command cargo --quiet install --git $url
    end

    echo
else
    error "Rust is not installed. It should be installed via Homebrew."
end

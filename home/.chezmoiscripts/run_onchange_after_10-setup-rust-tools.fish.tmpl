#!{{ lookPath "fish" }}

# Various clean-up steps.
# vim:ft=fish

# TODO: Move some/all of this to .chezmoiexternals or install-release ?

source "$CHEZMOI_SOURCE_DIR/.chezmoiscripts/.00_helpers.fish"

if not type -q rustup
    has_brew
    glog info "Installing rustup via Homebrew."
    brew install -q rustup-init
end

if not test -d "$HOME/.rustup"
    glog start "Running 'rustup update'"
    command rustup update >/dev/null 2>&1
    glog success "Rust toolchains are up to date."
end

# Add via 'install-release' https://github.com/glotlabs/gdrive

# Install packages via Cargo.
set -l PACKAGES \
    # https://github.com/Timmmm/autorebase
    autorebase \
    # https://github.com/RustSec/rustsec/tree/main/cargo-audit
    cargo-audit \
    # https://github.com/cargo-bins/cargo-binstall
    cargo-binstall \
    # https://github.com/matthiaskrgr/cargo-cache
    cargo-cache \
    # https://github.com/Riey/cargo-feature
    cargo-feature \
    # https://nexte.st
    cargo-nextest \
    # https://github.com/kbknapp/cargo-outdated
    cargo-outdated \
    # https://github.com/cargo-bins/cargo-quickinstall
    cargo-quickinstall \
    # https://github.com/birkenfeld/fddf
    fddf \
    # https://github.com/gitext-rs/git-dive
    git-dive \
    # https://github.com/nguyenvukhang/gitnu - git status wrapper.
    gitnu \
    # https://github.com/scruffystuffs/jfmt.rs
    jfmt \
    # https://github.com/mrjackwills/havn
    havn \
    # https://github.com/acidnik/ppcp - Remove in favor of xcp?
    ppcp \
    # https://github.com/TheWaWaR/simple-http-server
    simple-http-server \
    # https://github.com/tarka/xcp/
    xcp

switch (uname)
    case Linux
        # Only works on Linux.
        # https://github.com/Canop/lfs
        if not type -q lfs
            set -a PACKAGES lfs
        end
end

# These packages don't map to their CLI name.
# https://github.com/killercup/cargo-edit
if not type -q cargo-add
    set -a PACKAGES cargo-edit
end

# https://github.com/nabijaczleweli/cargo-update
if not type -q cargo-install-update
    set -a PACKAGES cargo-update
end

# https://github.com/robiot/rustcat
if not type -q rcat
    set -a PACKAGES rustcat
end

task "Installing Rust tools. ðŸ¦€"

for package in $PACKAGES
    sub_task "Installing $package"

    if not type -q $package
        command cargo --quiet install $package
    end
end

# My tools / forked.
set -l URLS \
    git+ssh://git@github.com/dsully/git-open \
    git+ssh://git@github.com/dsully/lolcate-rs \
    git+ssh://git@github.com/dsully/zoned

echo

for url in $URLS
    sub_task "Installing $url" "â—†"

    command cargo --quiet install --git $url
end

echo
glog success "Finished installing Rust tools."

#!{{ lookPath "fish" }}

# Install Rust tools.
# vim:ft=fish

source "{{ .chezmoi.sourceDir }}/.chezmoiscripts/.00_helpers.fish"

if not command -q cargo; and command -q rustup
    start "Installing Rust Toolchain.."

    command rustup toolchain install stable >/dev/null 2>&1
end

function install --description "Install a Rust package with optional URL and binary check"
    argparse 'p/package=' 'u/url=' 'b/binary=' -- $argv

    if not set -q _flag_package
        echo "Error: --package is required."
        return 1
    end

    # Set variables from parsed arguments
    set -l package $_flag_package
    set -l url $_flag_url
    set -l binary $_flag_binary
    set -l branch $_flag_branch

    # If binary is provided, check if it exists
    if test -n "$binary"
        if command -q $binary
            return
        end
    else
        if command -q $package
            return
        end
    end

    sub_task "Installing $package"

    set args cargo binstall --disable-telemetry -q -y $package

    if test -n "$url"
        set -a args --git $url
    end

    eval $args
end

if command -q cargo

    task "Installing Rust tools. ðŸ¦€"

    # Install packages via Cargo.
    set -l PACKAGES \
        # https://github.com/RustSec/rustsec/tree/main/cargo-audit
        cargo-audit \
        # https://github.com/mainmatter/cargo-autoinherit
        cargo-autoinherit \
        # https://github.com/matthiaskrgr/cargo-cache
        cargo-cache \
        # https://github.com/JanLikar/cargo-clone
        cargo-clone \
        # https://github.com/axodotdev/cargo-dist
        cargo-dist \
        # https://github.com/cargo-bins/cargo-quickinstall
        cargo-quickinstall \
        # https://github.com/boshen/cargo-shear
        cargo-shear \
        # https://github.com/Canop/codesort
        codesort \
        # https://github.com/birkenfeld/fddf
        fddf \
        # https://github.com/mrjackwills/havn
        havn \
        # https://github.com/tarka/xcp/
        xcp

    # https://github.com/nabijaczleweli/cargo-update
    if not command -q cargo-install-update
        set -a PACKAGES cargo-update
    end

    if not command -q ds
        set -a PACKAGES dirstat-rs
    end

    for package in $PACKAGES
        install --package $package
    end

    # My personal tools and forks.
    install --package autorebase --url https://github.com/Timmmm/autorebase
    install --package git-trim --url https://github.com/dsully/git-trim
    install --package lolcate --url https://github.com/dsully/lolcate-rs

    if is_macos
        install --package safari --url https://github.com/alexwlchan/safari.rs
    end

    echo
else
    error "'rustup' is not installed. It should be installed via Homebrew."
end

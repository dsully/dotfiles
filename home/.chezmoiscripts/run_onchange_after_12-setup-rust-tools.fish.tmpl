#!{{ lookPath "fish" }}

# Install Rust tools.
# vim:ft=fish

source "{{ .chezmoi.sourceDir }}/.chezmoiscripts/.00_helpers.fish"

if not type -q cargo; and type -q rustup-init
    start "Installing Rust Toolchain.."

    command rustup-init --no-modify-path -q -y >/dev/null 2>&1
end

if type -q cargo

    # Install packages via Cargo.
    set -l PACKAGES \
        # https://github.com/RustSec/rustsec/tree/main/cargo-audit
        cargo-audit \
        # https://github.com/matthiaskrgr/cargo-cache
        cargo-cache \
        # https://github.com/Riey/cargo-feature
        cargo-feature \
        # https://github.com/cargo-bins/cargo-quickinstall
        cargo-quickinstall \
        # https://github.com/birkenfeld/fddf
        fddf \
        # https://github.com/mrjackwills/havn
        havn \
        # https://github.com/tarka/xcp/
        xcp

    # These packages don't map to their CLI name.
    # https://github.com/killercup/cargo-edit
    if not type -q cargo-add
        set -a PACKAGES cargo-edit
    end

    # https://github.com/nabijaczleweli/cargo-update
    if not type -q cargo-install-update
        set -a PACKAGES cargo-update
    end

    if not type -q ds
        set -a PACKAGES dirstat-rs
    end

    task "Installing Rust tools. ðŸ¦€"

    for package in $PACKAGES
        sub_task "Installing $package"

        if not type -q $package
            command cargo binstall -q -y $package
        end
    end

    # My tools / forked / git-only.
    set -l URLS \
        https://github.com/dsully/git-open \
        https://github.com/dsully/lolcate-rs \
        https://github.com/dsully/gitrel

    if is_macos
        set -a URLS https://github.com/alexwlchan/safari.rs
    end

    echo

    for url in $URLS
        sub_task "Installing $url" "â—†"

        command cargo --quiet install --git $url
    end

    # Clean up packages now handled by gitrel.
    set -a UNINSTALL \
        autorebase \
        cargo-binstall \
        cargo-nextest \
        cargo-outdated \
        cargo-sort \
        cloudflare-dns-updater \
        git-dive \
        gitnu \
        jfmt \
        macos-defaults \
        reddit-saves \
        rustcat \
        simple-http-server

    sub_task "Uninstalling old packages." "â—†"

    for bin in $UNINSTALL
        command cargo --quiet uninstall $bin >/dev/null 2>&1
    end

    echo
else
    error "Rust is not installed. It should be installed via Homebrew."
end

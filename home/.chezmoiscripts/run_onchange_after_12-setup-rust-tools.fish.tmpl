#!{{ lookPath "fish" }}

# Install Rust tools.
# vim:ft=fish

source "{{ .chezmoi.sourceDir }}/.chezmoiscripts/.00_helpers.fish"

if not command -q cargo; and command -q rustup
    start "Installing Rust Toolchain.."

    command rustup toolchain install stable >/dev/null 2>&1
end

if command -q cargo

    # Install packages via Cargo.
    set -l PACKAGES \
        # https://github.com/RustSec/rustsec/tree/main/cargo-audit
        cargo-audit \
        # https://github.com/mainmatter/cargo-autoinherit
        cargo-autoinherit \
        # https://github.com/matthiaskrgr/cargo-cache
        cargo-cache \
        # https://github.com/JanLikar/cargo-clone
        cargo-clone \
        # https://github.com/axodotdev/cargo-dist
        cargo-dist \
        # https://github.com/Riey/cargo-feature
        cargo-feature \
        # https://github.com/cargo-bins/cargo-quickinstall
        cargo-quickinstall \
        # https://github.com/boshen/cargo-shear
        cargo-shear \
        # https://github.com/TimonPost/cargo-unused-features
        cargo-unused-features \
        # https://github.com/Canop/codesort
        codesort \
        # https://github.com/birkenfeld/fddf
        fddf \
        # https://github.com/foriequal0/git-trim
        git-trim \
        # https://github.com/mrjackwills/havn
        havn \
        # https://github.com/tarka/xcp/
        xcp

    # https://github.com/nabijaczleweli/cargo-update
    if not command -q cargo-install-update
        set -a PACKAGES cargo-update
    end

    if not command -q ds
        set -a PACKAGES dirstat-rs
    end

    task "Installing Rust tools. ðŸ¦€"

    if command -q cargo-binstall
        set -f installer cargo binstall -q -y
    else
        set -f installer cargo install -q
    end

    for package in $PACKAGES
        sub_task "Installing $package"

        if not command -q $package
            command $installer $package
        end
    end

    # My tools / forked / git-only.
    echo
    sub_task "Installing autorebase" "â—†"
    command cargo install --git https://github.com/Timmmm/autorebase

    sub_task "Installing lolcate-rs" "â—†"
    command cargo install --git https://github.com/dsully/lolcate-rs

    if is_macos
        sub_task "Installing safari.rs" "â—†"
        command cargo install --git https://github.com/alexwlchan/safari.rs
    end

    echo
else
    error "'rustup' is not installed. It should be installed via Homebrew."
end

#!/usr/bin/env fish

if not type -q rustup
    echo "rustup needs to be installed via Homebrew first!"
    exit
end

if not test -d "$HOME/.rustup"
    command rustup update
end

# Install packages via Cargo.
set -l PACKAGES \
    # https://github.com/Timmmm/autorebase
    autorebase \
    # https://github.com/RustSec/rustsec/tree/main/cargo-audit
    cargo-audit \
    # https://github.com/cargo-bins/cargo-binstall
    cargo-binstall \
    # https://github.com/matthiaskrgr/cargo-cache
    cargo-cache \
    # https://github.com/Riey/cargo-feature
    cargo-feature \
    # https://nexte.st
    cargo-nextest \
    # https://github.com/kbknapp/cargo-outdated
    cargo-outdated \
    # https://github.com/cargo-bins/cargo-quickinstall
    cargo-quickinstall \
    # https://github.com/birkenfeld/fddf
    fddf \
    # https://github.com/gitext-rs/git-dive
    git-dive \
    # https://github.com/nguyenvukhang/gitnu - git status wrapper.
    gitnu \
    # https://github.com/scruffystuffs/jfmt.rs
    jfmt \
    # https://github.com/ur0/lolcat
    lolcat \
    # https://github.com/acidnik/ppcp - Remove in favor of xcp?
    ppcp \
    # https://github.com/tarka/xcp/
    xcp

switch (uname)
    case Linux
        # Only works on Linux.
        # https://github.com/Canop/lfs
        if not type -q lfs
            set -a PACKAGES lfs
        end
end

# These packages don't map to their CLI name.
# https://github.com/killercup/cargo-edit
if not type -q cargo-add
    set -a PACKAGES cargo-edit
end

# https://github.com/nabijaczleweli/cargo-update
if not type -q cargo-install-update
    set -a PACKAGES cargo-update
end

# https://github.com/ngirard/lolcate-rs
if not type -q lolcate
    set -a PACKAGES lolcate-rs
end

# https://github.com/robiot/rustcat
if not type -q rcat
    set -a PACKAGES rustcat
end

for package in $PACKAGES
    if type -q $package
        echo "$package already installed"
    else
        command cargo install $package
    end
end

if not type -q cloudflare-dns-updater
    cargo install --git git+ssh://git@github.com/dsully/zoned
end

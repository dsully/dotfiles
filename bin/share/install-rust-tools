#!/usr/bin/env fish

if not type -q rustup-init
    echo "rustup needs to be installed via Homebrew first!"
    exit
end

if not test -d "$HOME/.rustup"
    command rustup-init -y
end

# Install packages via Cargo.
set -l PACKAGES \
    autorebase \
    cargo-cache \
    cargo-nextest \
    fddf \
    # https://github.com/nguyenvukhang/gitnu - git status wrapper.
    gitnu \
    jfmt \
    lolcat \
    # Renamer: https://github.com/yaa110/nomino
    nomino \
    ppcp \
    # https://github.com/tamton-aquib/veldora - PDF password brute force
    veldora

switch (uname)
    case Linux
        # Only works on Linux.
        if not type -q lfs
            set -a PACKAGES lfs
        end
end

# These packages don't map to their CLI name.
if not type -q cargo-add
    set -a PACKAGES cargo-edit
end

if not type -q cargo-install-update
    set -a PACKAGES cargo-update
end

if not type -q ds
    set -a PACKAGES dirstat-rs
end

if not type -q lolcate
    set -a PACKAGES lolcate-rs
end

if not type -q rcat
    set -a PACKAGES rustcat
end

for package in $PACKAGES
    if type -q $package
        echo "$package already installed"
    else
        command cargo install $package
    end
end

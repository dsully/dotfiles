#!/usr/bin/env fish

if not type -q rustup
    echo "rustup needs to be installed via Homebrew first!"
    exit
end

if not test -d "$HOME/.rustup"
    command rustup update
end

# Install packages via Cargo.
set -l PACKAGES \
    autorebase \
    # https://github.com/RustSec/rustsec/tree/main/cargo-audit
    cargo-audit \
    # https://github.com/matthiaskrgr/cargo-cache
    cargo-cache \
    # https://github.com/Riey/cargo-feature
    cargo-feature \
    # https://nexte.st
    cargo-nextest \
    # https://github.com/kbknapp/cargo-outdated
    cargo-outdated \
    fddf \
    # https://github.com/gitext-rs/git-dive
    git-dive \
    # https://github.com/nguyenvukhang/gitnu - git status wrapper.
    gitnu \
    jfmt \
    lolcat \
    ppcp \
    # https://github.com/tamton-aquib/veldora - PDF password brute force
    veldora \
    # https://github.com/tarka/xcp/
    xcp

switch (uname)
    case Linux
        # Only works on Linux.
        if not type -q lfs
            set -a PACKAGES lfs
        end
end

# These packages don't map to their CLI name.
if not type -q cargo-add
    set -a PACKAGES cargo-edit
end

if not type -q cargo-install-update
    set -a PACKAGES cargo-update
end

if not type -q lolcate
    set -a PACKAGES lolcate-rs
end

if not type -q rcat
    set -a PACKAGES rustcat
end

for package in $PACKAGES
    if type -q $package
        echo "$package already installed"
    else
        command cargo install $package
    end
end

if not type -q cloudflare-dns-updater
    cargo install --git git+ssh://git@github.com/dsully/zoned
end
